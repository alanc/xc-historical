#!/bin/csh -f
#
# $XConsortium: run_InsPEX,v 5.2 91/02/16 10:04:50 rws Exp $
#
# Copyright 1990, 1991 by Sun Microsystems, Inc. and the X Consortium.
# 
#			 All Rights Reserved
# 
# Permission to use, copy, modify, and distribute this software and its 
# documentation for any purpose and without fee is hereby granted, 
# provided that the above copyright notice appear in all copies and that
# both that copyright notice and this permission notice appear in 
# supporting documentation, and that the names of Sun Microsystems,
# the X Consortium, and MIT not be used in advertising or publicity 
# pertaining to distribution of the software without specific, written 
# prior permission.  
# 
# SUN MICROSYSTEMS DISCLAIMS ALL WARRANTIES WITH REGARD TO THIS SOFTWARE, 
# INCLUDING ALL IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS, IN NO
# EVENT SHALL SUN MICROSYSTEMS BE LIABLE FOR ANY SPECIAL, INDIRECT OR
# CONSEQUENTIAL DAMAGES OR ANY DAMAGES WHATSOEVER RESULTING FROM LOSS OF
# USE, DATA OR PROFITS, WHETHER IN AN ACTION OF CONTRACT, NEGLIGENCE OR
# OTHER TORTIOUS ACTION, ARISING OUT OF OR IN CONNECTION WITH THE USE OR
# PERFORMANCE OF THIS SOFTWARE.

#####	You'll possibly want to change these....

#	Name of the server that we run.  (Used to scan for running process.)
set    SNAME=Xsun
#	(Emptied) directory to accept InsPEX's output -- which is ***LOTS**
setenv INSPEXDEST /tmp/inspex_test
#	Can set the root of all evils right here....
setenv XHOME ../../..
setenv PEXHOME ../..

###########################################################################

#	*Abs* paths to X includes/libraries gets set here (optional)
setenv XINCDIR `cd $XHOME/X11; pwd`
setenv XLIBDIR `cd $XHOME/lib; pwd`
#	Path to X utility programs gets added here
set path=($path /usr/bin/X11)
setenv DISPLAY localhost:0.0

# SET the following to run the tests using client-side structure storage
##setenv I_CLIENT_SIDE 1

# PEXLIBDIR, PEXINCDIR - directories containing the PEX includes and libs.
setenv PEXLIBDIR $PEXHOME/lib/PEX
setenv PEXINCDIR $XINCDIR/extensions
# SET the location of the api support files to override the default location
# default location is where the files are installed by a 'make install'
setenv PEXAPIDIR `cd $PEXHOME/lib/PEX/lib; pwd`

setenv INSPEXHOME $cwd

# kill running server if already running one
setenv SERVER_RESTART_CMD "cd $INSPEXHOME; startpex; sleep 5"
set SERVER_PROC=`\ps ax | sed "/egrep $SNAME/d" | \egrep $SNAME | tail -1`
if ( $#SERVER_PROC >= 1 ) then
#	A server is running.  Kill it.
	echo "run_InsPEX attempting to abort currently running server for testing."
	echo "run_InsPEX attempting to abort currently running server for testing." >& /dev/console
	echo "The server (PID=$SERVER_PROC[1]) will be killed in 30 seconds."
	echo "The server (PID=$SERVER_PROC[1]) will be killed in 30 seconds." >& /dev/console
	sleep 30
	kill $SERVER_PROC[1]
	sleep 15
#	Check to see that server has stopped
	set SERVER_PROC=`\ps ax | sed "/egrep $SNAME/d" | \egrep $SNAME | tail -1`
	if ( $#SERVER_PROC >= 1 ) then
#		A server is still running.  Abort testing
		echo "Unable to kill the currently running server.  Aborting InsPEX test run."
		exit 1
	endif
endif

# Start up a server
echo "run_InsPEX attempting to start new server for testing."
echo $SERVER_RESTART_CMD
echo $SERVER_RESTART_CMD | sh -s

# prepare destination test result area
mkdir $INSPEXDEST >& /dev/null
mv $INSPEXDEST/ilog* /tmp >& /dev/null

if (! -x newpgrp )  cc -o newpgrp newpgrp.c

newpgrp sh inspex.sh -clean_all
